###############################################################################################
# For help on syntax, variables and functions, please read the file: "plcSyntaxHelp.plc"
#
# PLC Functionality Demo:
#
#   Code Description:
#     1. Enable power on axis (mc_power())
#     2. Home at startup if needed (mc_home())
#     3. Move to position 10 (mc_move_abs())
#     5. Stop sequence if limit switch violation or errorCode on motion blocks
#     

var errorCode:=0;

##### 0. Put all initiations here
if(PLC_ID.firstscan){  
  println('Starting PLC motion example!!');
  #Ignore commands during startup sequence
  AX_ID.blockcom:=1;
  static.seqStep:=1;
  static.cycleCounter:=0;
  static.epicsDisableSeq:=0;  #For disable over epics
};

if(static.epicsDisableSeq){
  static.seqStep:=100;  #invalid
};

##### 1. Put power on axis (run mc_power())
if(static.seqStep==1)
{
  if(not(AX_ID.error))
  {
    errorCode=mc_power(1,1);
    if(errorCode)
    {
      println('Function mc_power() returned error: ', errorCode);
      PLC_ID.error:=errorCode;
    };
  }
  else
  {
    AX_ID.reset:=1;
  };
  if(AX_ID.drv.enabled){
    static.seqStep:=2;
    println('2. Function mc_home()  triggered!');
  }
};

##### 2. Do homing sequence (run mc_home())
if(static.seqStep==2)
{
  var homingSeq:=3;
  var velTwoardsCam:=20;
  var velOffCam:=4;
  errorCode=mc_home(1,1,homingSeq,velTwoardsCam,velOffCam);
  if(errorCode){
    println('Function mc_home() returned error: ', errorCode);
    PLC_ID.error:=errorCode;
  };
  if(AX_ID.enc.homed and not(mc_get_busy(1)))
  {
    static.seqStep:=3;
    println('3. Function mc_move_abs()  triggered!');
  };
};

##### 3. Trigger a absolute move 
if(static.seqStep==3){
  AX_ID.blockcom:=0;  #Allow EPICS control
  var posTarg:=10;
  var vel:=50;
  var acc:=100;
  var dec:=100;
  errorCode:=mc_move_abs(1,1,posTarg,vel,acc,dec);
  if(errorCode){
    println('Error: mc_move_abs() returned error: ',errorCode);
    PLC_ID.error:=errorCode;
  };
  if(AX_ID.traj.setpos==posTarg and not(mc_get_busy(1)))
  {
    mc_move_abs(1,0,posTarg,vel,acc,dec);  # Set execute low
    PLC_ID.enable:=0;          # Disable PLC
  };
};

##### 5. Disable power if any of the limits are engaged
if((static.seqStep>2 and (not(AX_ID.mon.lowlim) or not(AX_ID.mon.highlim))) or static.epicsDisableSeq or errorCode)
{
  errorCode=mc_power(1,0);  # kill axis 1
  if(errorCode)
  {
    println('Function mc_power() returned error: ', errorCode);
    PLC_ID.error:=errorCode;
  };

  if(not(AX_ID.drv.enabled))
  {
    println('Sequence ended by limit switch violation. ErrorCode:', errorCode);
    static.seqStep:=10;      # Sequence end
    AX_ID.blockcom:=0;         # Allow commands again
    PLC_ID.enable:=0;          # Disable PLC
  };
};

return [];

println('NEVER HERE');
